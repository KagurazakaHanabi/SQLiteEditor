import java.text.SimpleDateFormat

apply plugin: 'com.android.application'

android {
    compileSdkVersion 27
    buildToolsVersion '27.0.3'
    defaultConfig {
        applicationId "com.yaerin.sqlite"
        minSdkVersion 21
        targetSdkVersion 27
        versionCode getMyVersionCode()
        versionName getMyVersionName()
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'com.android.support:support-annotations:27.1.1'
    implementation 'com.github.huangyanbin:SmartTable:2.2.0'
}

static def getMyVersionCode() {
    return Integer.parseInt(new SimpleDateFormat("yyMMdd").format(new Date()))
}

static def getMyVersionName() {
    return "1.0.3." + "git describe --always".execute().getText().trim()
}
